plugins {
    id 'com.android.application'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "deltazero.amarok"
        minSdkVersion 26
        targetSdkVersion 32
        versionCode 28
        versionName "0.8.2b1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-${getGitHash()}"
            debuggable true
        }
    }

    productFlavors {
        flavorDimensions "version"
        appcenter {
            dimension 'version'
        }
        foss {
            dimension 'version'
            applicationIdSuffix '.foss'
            versionNameSuffix '-foss'
        }
    }

    sourceSets {
        appcenter {
            java.srcDirs = ['src/appcenter/java']
        }
        foss {
            java.srcDirs = ['src/foss/java']
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Amarok-v${variant.versionName}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    namespace 'deltazero.amarok'
}

repositories {
    mavenCentral()
}

dependencies {
    def shizukuVersion = '12.2.0'
    implementation "dev.rikka.shizuku:api:${shizukuVersion}"
    implementation "dev.rikka.shizuku:provider:${shizukuVersion}"

    implementation 'com.github.heruoxin.Delegated-Scopes-Manager:client:master-SNAPSHOT'

    def appCenterSdkVersion = '5.0.0'
    appcenterImplementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    appcenterImplementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    appcenterImplementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"

    implementation 'com.github.getActivity:XXPermissions:13.2'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //    testImplementation 'junit:junit:4.+'
    //    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    //    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}